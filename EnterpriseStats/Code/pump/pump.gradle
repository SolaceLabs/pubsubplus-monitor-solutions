/**
 * Copyright 2016-2017 Solace Corporation. All rights reserved.
 *
 * http://www.solace.com
 *
 * This source is distributed under the terms and conditions of any contract or
 * contracts between Solace Corporation ("Solace") and you or your company. If
 * there are no contracts in place use of this source is not authorized. No
 * support is provided and no distribution, sharing with others or re-use of 
 * this source is authorized unless specifically stated in the contracts 
 * referred to above.
 *
 * This software is custom built to specifications provided by you, and is 
 * provided under a paid service engagement or statement of work signed between
 * you and Solace. This product is provided as is and is not supported by 
 * Solace unless such support is provided for under an agreement signed between
 * you and Solace.
 */
apply plugin: 'application'

group = 'core'
description = 'StatsPump core application'
mainClassName = "com.solace.psg.enterprisestats.statspump.StatsPump"
applicationName = 'statspump'

startScripts {
	// Temp hack as pump looks for semp schema files in classpath
	classpath += files('resources')
    // Hack to replace log4j2.properties with APP_HOME in start up scripts
	doLast {
		unixScript.text = unixScript.text.replace('config/log4j2.properties', '\$APP_HOME/config/log4j2.properties')
		windowsScript.text = windowsScript.text.replace('config/log4j2.properties', '%~dp0..\\config\\log4j2.properties')
		unixScript.text = unixScript.text.replace('$APP_HOME/lib/resources', '\$APP_HOME/resources')
		windowsScript.text = windowsScript.text.replace('%APP_HOME%\\lib\\resources', '%APP_HOME%\\resources')
	}
}

applicationDefaultJvmArgs = ['-Xmx512M', '-Dlog4j.configurationFile=config/log4j2.properties']

jar {
	baseName = 'solace-statspump'
    version =  version
    manifest {
        attributes 'Created-By': version+' (Solace Corporation)',
                   'Implementation-Title': 'Solace StatsPump',
                   'Implementation-Version': version
    }
}

distributions {
    main {
        baseName = "solace-statspump-bin"
        contents {
            from("${rootDir}") {
                include "LICENSE"
                include versionFile.name
            }
            from('config') {
                into 'config'
            }
            from('resources') {
                into 'resources'
            }
            from('scripts') {
                into 'scripts'
            }
            from('README.md')
        }
    }
}
