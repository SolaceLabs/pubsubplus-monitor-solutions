/**
 * Copyright 2016-2017 Solace Corporation. All rights reserved.
 *
 * http://www.solace.com
 *
 * This source is distributed under the terms and conditions of any contract or
 * contracts between Solace Corporation ("Solace") and you or your company. If
 * there are no contracts in place use of this source is not authorized. No
 * support is provided and no distribution, sharing with others or re-use of 
 * this source is authorized unless specifically stated in the contracts 
 * referred to above.
 *
 * This software is custom built to specifications provided by you, and is 
 * provided under a paid service engagement or statement of work signed between
 * you and Solace. This product is provided as is and is not supported by 
 * Solace unless such support is provided for under an agreement signed between
 * you and Solace.
 */
apply plugin: 'application'

group = 'core'
description = 'StatsPump listener framework'
mainClassName = "com.solace.psg.enterprisestats.receiver.StatsReceiver"
applicationName = 'stats-receiver'

startScripts {
    // Hack to replace log4j2.properties with APP_HOME in start up scripts and use short classpaths
    unixStartScriptGenerator.template = resources.text.fromFile('templates/customUnixStartScript.txt')
    windowsStartScriptGenerator.template = resources.text.fromFile('templates/customWindowsStartScript.txt')
    doLast {
        unixScript.text = unixScript.text.replace('config/log4j2.properties', '\$APP_HOME/config/log4j2.properties')
        windowsScript.text = windowsScript.text.replace('config/log4j2.properties', '%~dp0..\\config\\log4j2.properties')
    }
}

applicationDefaultJvmArgs = ['-Dlog4j.configurationFile=config/log4j2.properties']

jar {
    baseName = 'stats-receiver'
    version =  version
    manifest {
        attributes 'Created-By': version+' (Solace Corporation)',
                   'Implementation-Title': 'Solace StatsPump Listener Framework',
                   'Implementation-Version': version
    }
}

distributions {
    main {
        baseName = "solace-stats-receiver-bin"
        contents {
            from("${rootDir}") {
                include "LICENSE"
                include versionFile.name
            }
            from('config') {
                into 'config'
            }
            from('scripts') {
                into 'scripts'
            }
            from('README.md')
        }
    }
}